PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX so: <http://www.sequenceontology.org/miso/current_svn/term/SO:>

SELECT ?variantTitle ?diseaseTitle ?diseaseId ?source ?pubmed
WHERE {
	?gene sio:SIO_000205/dcterms:title "INSERT_GENE_SYMBOL_HERE"@en ; # Adjust "INSERT_GENE_SYMBOL_HERE" to actual gene symbol!
	dcterms:identifier ?geneId .
	{
		?association sio:SIO_000628 ?gene .

		FILTER EXISTS { ?association rdf:type/rdfs:subClassOf* sio:SIO_000983 }
	}
	UNION
	{
		?variant so:associated_with ?gene ;
		dcterms:title ?variantTitle .
		?association sio:SIO_000628 ?variant .
		
		FILTER EXISTS { ?association rdf:type sio:SIO_000897 }
	}
	?association sio:SIO_000253 ?source ;
	sio:SIO_000628 ?disease .
	?disease dcterms:identifier ?diseaseId ;
	dcterms:title ?diseaseTitle .
	OPTIONAL { ?association sio:SIO_000772 ?pubmed }

	FILTER EXISTS { ?gene rdf:type ncit:C16612 }
	FILTER EXISTS { ?disease rdf:type ncit:C7057 }
	FILTER EXISTS { ?source rdf:type dctypes:Dataset }
}
ORDER BY ?variantTitle ?diseaseTitle ?source ?pubmed